plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'com.github.izzyreal'
version '1.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.googlecode.soundlibs', name: 'tritonus-share', version: '0.3.7.4'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'toot2'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'toot2'
                description = 'Audio, MIDI and Music Theory models as reusable components'
                url = 'https://github.com/izzyreal/toot2'
                licenses {
                    license {
                        name = 'GNU General Public License, version 2'
                        url = 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'izzyreal'
                        name = 'Izmar Verhage'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/izzyreal/toot2.git'
                    developerConnection = 'scm:git:ssh://github.com/izzyreal/toot2.git'
                    url = 'http://github.com/izzyreal/toot2/'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html4', true)
    }
}

signing {
    if (project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile')) {
        sign publishing.publications.mavenJava
    }
}
